apply plugin: 'com.android.application'

//Path to 3rd-party precompiled C/C++ libraries
def dependenciesDir = "${projectDir}/../../../Dependencies"
def dependenciesLibDir = "${dependenciesDir}/lib"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.fogchain.radjavvm"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -fexceptions"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            ndk {
                abiFilters "x86" //"armeabi-v7a", "arm64-v8a", "x86_64",
            }
        }
        debug {
            jniDebuggable true
            ndk {
                abiFilters "x86" //"armeabi-v7a", "arm64-v8a", "x86_64",
            }
        }
    }

    signingConfigs {
        config {
            //v2SigningEnabled false
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    productFlavors {
    }

    sourceSets {
        main {
            jniLibs.srcDirs dependenciesLibDir
        }
    }
}

task copyV8FilesToAssets(type: Copy) {
    println("Copying V8 precompiled files to assets folder")

    from dependenciesLibDir
    into "${projectDir}/src/main/assets/v8"
    include "**/natives_blob.bin"
    include "**/snapshot_blob.bin"
}

//task copyExamplesToAssets(type: Copy){
//    println("Copying examples to assets folder")
//    from "../../examples"
//    into "${projectDir}/src/main/assets/examples"
//}

task archiveExamplesAndPutToAssets(type: Zip) {
    from "../../examples"
    destinationDir = file("${projectDir}/src/main/assets")
    archiveName = "examples.zip"
}

project.afterEvaluate {
    preBuild.dependsOn copyV8FilesToAssets
    //preBuild.dependsOn copyExamplesToAssets
    preBuild.dependsOn archiveExamplesAndPutToAssets
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
